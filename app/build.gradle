plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'jacoco'
    id 'com.google.gms.google-services'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.6.21'
}

tasks.whenTaskAdded { task ->
    if (task.name.contains("ReleaseUnitTest")) {
        task.enabled = false
    }
}

jacoco {
    toolVersion = "0.8.7"
}

android {
    compileSdk 32

    defaultConfig {
        applicationId 'com.github.sdpsharelook'
        minSdk 23
        targetSdk 32
        versionCode 1
        versionName '0.1'

        testInstrumentationRunner "com.github.sdpsharelook.MyTestRunner"
    }


    buildFeatures {
        viewBinding true
        dataBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    testOptions {
        animationsDisabled true
        unitTests {
            includeAndroidResources true
        }

    }
    namespace 'com.github.sdpsharelook'
}

dependencies {
    implementation platform('com.google.firebase:firebase-bom:30.1.0')
    final def hilt = '2.42'
    final def fragment_version = '1.4.1'

    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.core:core-ktx:1.8.0' // enable modern kotlin features
    implementation 'androidx.core:core-splashscreen:1.0.0-rc01'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.fragment:fragment-ktx:$fragment_version"
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.2'
    implementation 'androidx.test.espresso:espresso-idling-resource:3.4.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.2'
    implementation 'androidx.navigation:navigation-testing:2.4.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.2'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.test.espresso:espresso-idling-resource:3.4.0'
    implementation 'io.coil-kt:coil:2.1.0'
    implementation 'com.google.android.gms:play-services-maps:18.0.2'
    implementation 'com.google.android.gms:play-services-mlkit-text-recognition:18.0.0'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation "com.google.dagger:hilt-android:$hilt"
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-database-ktx'
    implementation 'com.google.firebase:firebase-functions'
    implementation 'com.google.firebase:firebase-storage-ktx'
    implementation 'com.google.mlkit:language-id:17.0.3'
    implementation 'com.google.mlkit:translate:17.0.0'
    implementation 'com.makeramen:roundedimageview:2.3.0'
    implementation "io.coil-kt:coil:2.1.0"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.6.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-datetime:0.3.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.3'
    testImplementation 'androidx.test:core-ktx:1.4.0' // necessary for kotlin tests
    testImplementation 'androidx.test:rules:1.4.0'
    testImplementation 'androidx.test:runner:1.4.0'
    testImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    testImplementation 'androidx.test.espresso:espresso-intents:3.4.0'
    testImplementation 'androidx.test.ext:junit:1.1.3'
    testImplementation "com.google.dagger:hilt-android-testing:$hilt"
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.2'
    testImplementation 'org.mockito:mockito-core:4.6.0'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:4.0.0'
    testImplementation 'org.robolectric:robolectric:4.8.1' // add all test dependencies
    androidTestImplementation 'androidx.test:core-ktx:1.4.0' // necessary for kotlin tests
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt"
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.2'
    debugImplementation "androidx.fragment:fragment-testing:$fragment_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt"
    kaptTest "com.google.dagger:hilt-android-compiler:$hilt"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt"
}


configurations.all {
    resolutionStrategy {
        eachDependency { details ->
            if ('org.jacoco' == details.requested.group) {
                details.useVersion "0.8.7"
            }
        }
    }
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {
    group = 'Reporting'
    description = 'Generate Jacoco coverage reports after running tests.'
    reports {
        xml.required = true
        html.required = true
    }

    def fileFilter = [
            // data binding
            'android/databinding/**/*.class',
            '**/android/databinding/*Binding.class',
            '**/android/databinding/*',
            '**/androidx/databinding/*',
            '**/databinding/*',
            '**/BR.*',
            // android
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*Test*.*',
            'android/**/*.*',
            // kotlin
            '**/*MapperImpl*.*',
            '**/BuildConfig.*',
            '**/*BR*.*',
            '**/Manifest*.*',
            '**/*$Lambda$*.*',
            '**/*$Lambda*.*',
            '**/*Companion*.*',
            '**/*Module*.*',
            '**/*Extensions*.*',
            // Exclude Hilt generated classes
            '**/*Dagger*.*',
            '**/*Hilt*.*',
            'hilt_aggregated_deps/**',
            '**/*Component*.*',
            '**/*_Factory.class',
            '**/*_Factory**',
            '**/*_Factory*.*',
            '**/*_Provide*Factory*.*',
            '**/*_MembersInjector.class',
            '**/*_MembersInjector**',
            '**/di/*',
            // Android Navigation
            '**/*Args*',
            '**/*Directions*'
    ]

    def javaTree = fileTree(dir: "${project.buildDir}/intermediates/javac/debug", excludes: fileFilter)
    def kotlinTree = fileTree(dir: "${project.buildDir}/tmp/kotlin-classes/debug", excludes: fileFilter)

    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories.from = (files([mainSrc]))
    classDirectories.from = (files([kotlinTree], [javaTree]))
    executionData.setFrom(fileTree(dir: project.buildDir, includes: [
            'outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec',
            'outputs/code_coverage/debugAndroidTest/connected/*/coverage.ec',
            'outputs/code_coverage/debugAndroidTest/connected/*coverage.ec'
//            ,'/jacoco/testDebugUnitTest.exec'
    ]))
}

connectedCheck {
    finalizedBy jacocoTestReport
}

// Allow references to generated code
kapt {
    correctErrorTypes true
}